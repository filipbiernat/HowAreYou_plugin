apply plugin: 'com.android.application'

android {
    compileSdkVersion compile_sdk
    buildToolsVersion build_tools

    defaultConfig {
        applicationId "com.aware.plugin.howareyou" //EDIT ME
        minSdkVersion minimum_sdk
        targetSdkVersion target_sdk
        versionCode version_code
        versionName version_readable
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

if (System.getenv("storeFile") != null && System.getenv("storePassword") != null && System.getenv("keyAlias") != null && System.getenv("keyPassword") != null) {
    android.signingConfigs.release.storeFile = file(System.getenv("storeFile"))
    android.signingConfigs.release.storePassword = System.getenv("storePassword")
    android.signingConfigs.release.keyAlias = System.getenv("keyAlias")
    android.signingConfigs.release.keyPassword = System.getenv("keyPassword")
} else {
    println("No keystore defined. Unsigned!")
    android.buildTypes.release.signingConfig = null
}

repositories {
    maven { url 'https://jitpack.io' } //this allows library generation directly from GitHub source-code
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    //Parent AWARE libraries dependencies
    implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$mqtt_libs"
    implementation "com.koushikdutta.ion:ion:$ion_libs"
    implementation "com.android.support:appcompat-v7:$support_libs"
    implementation "com.android.support:gridlayout-v7:$support_libs"

    api "com.github.denzilferreira:aware-client:$aware_libs"
    //To debug core replace with below. Remember to uncomment in settings.gradle
    //implementation project(':aware-core')

    //Android Face Detection
    implementation 'com.google.android.gms:play-services-vision:11.8.0'

    // Use the following line to include client library for Face API from Maven Central Repository
    implementation 'com.microsoft.projectoxford:face:1.4.3'

    implementation 'com.github.QuadFlask:colorpicker:0.0.13'

    //Filters from https://github.com/berndporr/iirj
    implementation group: 'uk.me.berndporr', name:'iirj', version: '1.1'

    //heartdroid-1.3.0.jar for heart-aware
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //Job dispatcher for heart-aware
    compile 'com.firebase:firebase-jobdispatcher-with-gcm-dep:0.5.2'


    //Slidable actividy for closure on slide: https://github.com/r0adkll/Slidr
    implementation 'com.r0adkll:slidableactivity:2.0.6'

}
